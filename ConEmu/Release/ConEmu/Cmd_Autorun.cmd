@echo off

rem This (Cmd_Autorun /i) will install to the registry following keys
rem   [HKEY_CURRENT_USER\Software\Microsoft\Command Processor]
rem   "AutoRun"="\"C:\\Program Files\\FAR\\cmd_autorun.cmd\""
rem Which cause auto attaching to ConEmu each started cmd.exe or tcc.exe
rem If ConEmu (GUI) was not started yes, new instance will be started.


if "%1"=="" goto noparm
goto checkparm

:noparm
rem чтобы не раздражать лишним текстом, когда ConEmu уже запущено...
rem так определять не совсем корректно. cmd могли запустить из ConEmu
rem в режиме создания новой консоли... ну да ладно, пока так...
if "%ConEmuHWND%"=="" goto noconemu
goto conemufound


:checkparm
if "%1"=="/?" goto help
if "%1"=="-?" goto help

if "%1"=="/i" goto install
if "%1"=="-i" goto install

if "%1"=="/u" goto uninstall
if "%1"=="-u" goto uninstall

rem Под телнетом - не запускать!
if not "%TERM%"=="" goto termfound

rem Проверить, может ConEmu уже запущено?
rem Warning!!! Это не работает, если в ConMan(!) указан
rem "CreateInNewEnvironment"=dword:00000001
rem Нужно так:
rem REGEDIT4
rem [HKEY_CURRENT_USER\Software\HoopoePG_2x]
rem "CreateInNewEnvironment"=dword:00000000

:noconemu
set ConEmuPath=%~dp0ConEmuC.exe
if not exist "%ConEmuPath%" set ConEmuPath=ConEmuC.exe
if not exist "%ConEmuPath%" goto notfound
echo ConEmu autorun (c) Maximus5
echo Starting "%ConEmuPath%" in "Attach" mode
call "%ConEmuPath%" /ATTACH /NOCMD
echo Done
goto fin

:notfound
Echo ConEmu not found! "%~dp0ConEmuC.exe"
goto fin
:gui_notfound
Echo ConEmu not found! "%~dp0..\ConEmu.exe"
goto fin
:termfound
Echo ConEmu can not be started under Telnet!
goto fin
:conemufound
rem Echo ConEmu already started (%ConEmuHWND%)
goto fin

:help
echo ConEmu autorun (c) Maximus5
echo Usage
echo    cmd_autorun.cmd /?  - displays this help
echo    cmd_autorun.cmd /i  - turn ON ConEmu autorun for cmd.exe
echo    cmd_autorun.cmd /u  - turn OFF ConEmu autorun
goto fin

:install
set ConEmuPath=%~dp0..\ConEmu.exe
if exist "%ConEmuPath%" goto install_found
set ConEmuPath=%~dp0ConEmu.exe
if exist "%ConEmuPath%" goto install_found
set ConEmuPath=%~dp0..\ConEmu64.exe
if exist "%ConEmuPath%" goto install_found
set ConEmuPath=%~dp0ConEmu64.exe
if exist "%ConEmuPath%" goto install_found
goto gui_notfound
:install_found
echo ConEmu autorun (c) Maximus5
echo Enabling autorun
call "%ConEmuPath%" /autosetup 1 "%~0"
goto CheckRet

:uninstall
set ConEmuPath=%~dp0..\ConEmu.exe
if exist "%ConEmuPath%" goto uninstall_found
set ConEmuPath=%~dp0ConEmu.exe
if exist "%ConEmuPath%" goto uninstall_found
set ConEmuPath=%~dp0..\ConEmu64.exe
if exist "%ConEmuPath%" goto uninstall_found
set ConEmuPath=%~dp0ConEmu64.exe
if exist "%ConEmuPath%" goto uninstall_found
goto gui_notfound
:uninstall_found
echo ConEmu autorun (c) Maximus5
echo Disabling autorun
call "%ConEmuPath%" /autosetup 0
goto CheckRet


:CheckRet
if errorlevel 2 echo ConEmu failed
if errorlevel 1 echo All done
goto fin

:fin
